# Devoir 3 - IFT 1215
# 4 avril 2024 - Équipe T
# Mathieu Ducharme, Thomas Coté, Thomas Racine

# Ce programme LMC convertit un nombre en base vers une
# base comprise entre 2 et 9 (inclusivement).

# Prend les valeurs en input
	IN		
	STO	num	# num = input('Nbr en base 10 a convertir)
	IN
	STO	base	# base = input('Base vers laquelle convertir)
	
# S'assure que la base est valide (entre 2 et 9)
	SUB	ten	# acc = base - 10
	BRP	EXIT	# base - 10 >= 0 ? ferme le programme : continue
	LDA	one	# acc = 1
	SUB	base	# acc = 1 - base
	BRP	EXIT	# 1 - base >= 0 ? ferme le programme : continue
	
# Retourne 0 si le nombre est 0 en base 10
	LDA	num
	BRZ	zeroOut	# num == 0 ? zeroOut() : continue

# *Convertision du nombre*
#  while num > 0
WHILE	LDA	minus	# minus = -1
	STO	divCtr	# Initialise la var d'itération a -1
	
# Modulo: result = num % base
	LDA	num	# Initiallement result = num...
MODULO	STO	result	# sinon result = result - base
	LDA	result
	SUB	base	
	BRP	MODULO	# result - base >= 0 ? goto MODULO : continue

# Enregistre dans le tableau de resultat du modulo (num % base)
	LDA	result
save	STO	tabStart
	LDA	save
	ADD	one
	STO	save

# Division: num = num // base
DIV	LDA	divCtr
	ADD	one
	STO	divCtr	# divCtr += 1
	LDA	num
	SUB	base
	STO	num	# num = num - base
	BRP	DIV	# num - base >= 0 ? goto DIV : continue

	LDA	divCtr
	STO	num	# num = num // base
	BRZ	print	# num == 0 ? print(tab) : continue
	BRP	WHILE	# num > 0 ? goto WHILE : continue
	HLT		# Sort du programme dans le cas ou num est negatif
				
# Print le tableau de resultat
print	LDA	tabEnd
	SUB	ten
	BRP	noPrint	# tab[i] < 10 ? dont print : print
	ADD	ten
	OUT		# print(tab[i])
noPrint	LDA	cmp
	ADD	one
	STO	cmp	# cmp += 1
	SUB	len
	BRZ	EXIT	# arrete le programme si cmp a parcourru toute la liste
	LDA	print
	SUB	one
	STO	print
	BR	print
	

EXIT	HLT


zeroOut	LDA	zero
	OUT
	HLT
	




# Variables
num	DAT	000
base	DAT	000
divCtr	DAT	000
result	DAT	000

# Constantes
minus	DAT	-1
zero	DAT	0
one	DAT	1
ten	DAT	10

# Tableau parametres
len	DAT	9	# = 9
cmp	DAT	0

# Tableau valeurs
tabStart	DAT	0
	DAT	10
	DAT	10
	DAT	10
	DAT	10
	DAT	10
	DAT	10
	DAT	10
tabEnd	DAT	10
